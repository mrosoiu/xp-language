# XP Framework Macro file for Nedit
#
# $Id$
#

# Copy this file to $HOME/.nedit/xp.nm

define syntaxcheck_php {
  res = shell_command("php $HOME/bin/lintphp.php", get_range(0,$text_length))
  if (res != "" ) {
     pt = search_string(res, "\n", 0)
     er_line = substring(res, 0, pt)
     pt = pt + 1
     pt2 = search_string(res, "\n", pt)
     er_name = substring(res, pt, pt2)
     goto_line_number(er_line)
     end = search("$", $cursor, "regex")
     select($cursor, end)
     calltip(er_name, end, "tipText")
  } else {
    kill_calltip()
  }
}

define syntaxcheck_xml {
  res = shell_command("xmllint -noout -", get_range(0,$text_length))
  if (res != "" ) {
     pt = search_string(res, ":", 0)
     er_file= substring(res, 0, pt)
     pt = pt + 1
     pt2 = search_string(res, ":", pt)
     er_line = substring(res, pt, pt2)
     pt = pt2 + 1
     pt2 = search_string(res, "\n", pt)
     er_text = substring(res, pt, pt2)
     goto_line_number(er_line)
     end = search("$", $cursor, "regex")
     select($cursor, end)
     calltip(er_text, end, "tipText")
  } else {
    kill_calltip()
  }
}

define syntaxcheck_sh {
  tmpfile= shell_command("mktemp -t nedit", "")
  shell_command("cat - > " tmpfile, get_range(0, $text_length))
  res= shell_command("/bin/sh -n " tmpfile, "")
  shell_command("rm " tmpfile, "")
  
  if (res != "") {
    pt= search_string(res, ":", 0) + 1
    pt2= search_string(res, ":", pt)
    err_line= substring(res, pt, pt2)
    pt= pt2 + 1
    pt2= search_string(res, "\n", pt)
    err_text= substring(res, pt, pt2)
    goto_line_number(err_line)
    end= search("$", $cursor, "regex")
    select($cursor, end)
    calltip(err_text, end, "tipText")
  } else {
    kill_calltip()
  }
}

define open_file_xpclass {
  res= shell_command("php $HOME/bin/findclass.php " get_selection(), "")
  if ("Class/sapi" != substring(res, 0, 10)) {
    filename= split(res, "\n")
    open(filename[0])
  } else {
    calltip(res, -1, "tipText")
  }
}

define create_xp_class {
  include_file( getenv("HOME") "/.nedit/templates/xpclass.tmpl")
  set_language_mode("PHP")
  goto_line_number(14)
}

define create_xp_cli {
  include_file( getenv("HOME") "/.nedit/templates/xpcli.tmpl")
  set_language_mode("PHP")
  goto_line_number(24)
}

define create_xp_testcase {
  include_file( getenv("HOME") "/.nedit/templates/xptestcase.tmpl" )
  set_language_mode("PHP")
  goto_line_number(17)
}

define add_xp_methoddoc {
  insert_string(read_file(getenv("HOME") "/.nedit/templates/xpmethoddoc.tmpl"))
}

define add_xp_classheader {
  insert_string(read_file(getenv("HOME") "/.nedit/templates/xpclassheader.tmpl"))
}

define add_xp_testmethod {
  insert_string(read_file(getenv("HOME") "/.nedit/templates/xptestmethod.tmpl"))
}

define execute_svn_diff {
  result= shell_command("svn diff " $file_path "/" $file_name, "")
  if ($shell_cmd_status != 0) {
    dialog(result, "Ok")
  } else {
    new()
    focus_window("last")
    insert_string(result)
    set_language_mode("Unified diff")
  }
}

define execute_svn_ci {
  result= string_dialog("Please specify the commit message:", "Ok", "Abort")
  if ($string_dialog_button == 1) {
    if (1 == write_file(result, "/tmp/nedit.svn-ci-msg.tmp")) {
      cmdres= shell_command("svn ci -F /tmp/nedit.svn-ci-msg.tmp " $file_path "/" $file_name, "")
      dialog(cmdres, "Ok")
    } else {
      dialog("Writing commit msg failed.", "Ok")
    }
  }
}

define open_shell_for_file {
  shell_command(getenv("HOME") "/.nedit/runshell.sh", "")
}

define open_browser {
  url= $1
  
  browser= getenv("BROWSER")
  if (length(browser) == 0) {
    browser= "firefox"
  }
  
  res= shell_command(browser " '" url "'", "")
  if (res != "") dialog("Executing results: " res)
}

define browse_help_php {
  open_browser("http://php3.de/" $1)
}

define browse_help_xp {
  open_browser("http://xp-framework.net/search.php?keyword=" $1)
}

define browse_help_java {
  open_browser("http://javadoc.xp-framework.net/search/?pattern=" $1 "&selected=c&results=10&go=Go")
}
